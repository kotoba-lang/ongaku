(ns ongaku.compose-test
  (:require [clojure.test :refer [deftest is testing]]
            [ongaku.catalog :as catalog]
            [ongaku.compose :as compose]
            [ongaku.policy :as policy]))

(def ^:private asset-defaults
  {:policy/render-only? true
   :policy/raw-public-access? false
   :policy/ai-training? false
   :policy/content-id-registration? false
   :license/attribution-required? false})

(def fixture
  {:ongaku.catalog/version 1
   :ongaku.catalog/assets
   [(merge asset-defaults
           {:asset/id "fx-calm"
            :asset/title "Calm Fixture"
            :asset/local-path "assets/fx-calm.mp3"
            :asset/source :source/fixture
            :audio/duration-sec 180.0
            :music/moods #{:calm :daily}
            :music/channels #{"demo"}
            :credit/text "BGM: Calm Fixture / tester"})
    (merge asset-defaults
           {:asset/id "fx-upbeat"
            :asset/title "Upbeat Fixture"
            :asset/local-path "assets/fx-upbeat.mp3"
            :asset/source :source/fixture
            :audio/duration-sec 90.0
            :music/moods #{:upbeat}
            :music/channels #{"demo" "other"}
            :credit/text "BGM: Upbeat Fixture / tester"})]})

(deftest catalog-reads-both-key-shapes
  (is (= 2 (count (catalog/assets fixture))))
  (let [legacy {:ongakuka.catalog/assets (:ongaku.catalog/assets fixture)}]
    (is (= 2 (count (catalog/assets legacy)))))
  (is (= "fx-calm" (:asset/id (catalog/by-id fixture "fx-calm")))))

(deftest compose-selects-render-only-plan
  (let [plan (compose/compose fixture {:channel-id "demo"
                                       :duration/sec 120
                                       :mood :calm})]
    (is (= :plan/background-music (:ongaku/plan plan)))
    (is (= "fx-calm" (:asset/id plan)))
    (is (= "assets/fx-calm.mp3" (:asset/path plan)))
    (is (:credit/text plan))
    (is (true? (:policy/render-only? plan)))
    (is (false? (:policy/raw-public-access? plan)))))

(deftest compose-ranks-by-requested-mood
  (let [plan (compose/compose fixture {:channel-id "demo" :mood :upbeat})]
    (is (= "fx-upbeat" (:asset/id plan)))))

(deftest compose-throws-when-nothing-allowed
  (is (thrown? #?(:clj Exception :cljs :default)
               (compose/compose fixture {:channel-id "unknown-channel"
                                         :mood :calm}))))

(deftest policy-rejects-raw-file-exposure
  (let [asset (catalog/by-id fixture "fx-calm")]
    (testing "raw exposure, AI training, and Content ID registration are refused"
      (is (seq (policy/policy-errors asset {:channel-id "demo"
                                            :usage-context :youtube-background
                                            :expose-raw? true})))
      (is (seq (policy/policy-errors asset {:channel-id "demo"
                                            :usage-context :youtube-background
                                            :ai-training? true})))
      (is (seq (policy/policy-errors asset {:channel-id "demo"
                                            :usage-context :youtube-background
                                            :content-id? true}))))
    (testing "allowed request has no errors"
      (is (empty? (policy/policy-errors asset {:channel-id "demo"
                                               :usage-context :youtube-background}))))))
